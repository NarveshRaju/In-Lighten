#include <Wire.h>
#include <LiquidCrystal_I2C.h>

LiquidCrystal_I2C lcd(0x27, 16, 2);

#define TRIGGER_S1 12
#define ECHO_S1 11
#define TRIGGER_S2 4
#define ECHO_S2 5
#define RELAY_PIN 8

long dis_a = 0, dis_b = 0;
int person = 0;

void setup() {
  Serial.begin(9600);
  pinMode(TRIGGER_S1, OUTPUT);
  pinMode(ECHO_S1, INPUT);
  pinMode(TRIGGER_S2, OUTPUT);
  pinMode(ECHO_S2, INPUT);
  pinMode(RELAY_PIN, OUTPUT);

  lcd.init();
  lcd.backlight();
  lcd.setCursor(0, 0);
  lcd.print(" GREETINGS FROM    ");
  lcd.setCursor(0, 1);
  lcd.print(" IN-LIGHTEN     ");
  delay(2000);
  lcd.clear();
}

void ultra_read(int triggerPin, int echoPin, long &distance) {
  digitalWrite(triggerPin, LOW);
  delayMicroseconds(2);
  digitalWrite(triggerPin, HIGH);
  delayMicroseconds(10);
  digitalWrite(triggerPin, LOW);
  distance = pulseIn(echoPin, HIGH) / 29 / 2;
}

void updateSensors() {
  ultra_read(TRIGGER_S1, ECHO_S1, dis_a);
  delay(30);
  ultra_read(TRIGGER_S2, ECHO_S2, dis_b);
  delay(30);
 Serial.print("da:");Serial.println(dis_a);
 Serial.print("db:");Serial.println(dis_b);   
}

void displayPersonCount() {
  lcd.setCursor(0, 0);
  lcd.print("People: ");
  lcd.print(person);
}

void controlLight() {
  lcd.setCursor(0, 1);
  lcd.print("Light: ");

  if (person > 0) {
    digitalWrite(RELAY_PIN, LOW); // Turn on the light
    lcd.print("On ");
  } else {
    digitalWrite(RELAY_PIN, HIGH); // Turn off the light
    lcd.print("Off");
  }
}

void loop() {
  updateSensors();
  controlLight();
  displayPersonCount();

  if (dis_a < 40 && dis_b >= 40) {
    person++;
    displayPersonCount();
    controlLight();
    delay(1000);
  } else if (dis_b < 40 && dis_a >= 40) {
    if (person > 0) {
      person--;
      displayPersonCount();
      controlLight();
      delay(1000); 
    }
  }
}
